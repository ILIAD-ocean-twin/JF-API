#+ summary: "Retrieves all the information about ILIAD Observations for a specific Datastream"
#+ description: "NOTE: make sure response content type is application/json"
#+ endpoint: "https://www.foodie-cloud.org/sparql?default-graph-uri=http://w3id.org/iliad/dataaset/jf-pilot-sample"
#+ tags:
#+   - json
#+ method: GET
#+ limit: 100
#+ offset: 0
#+ endpoint_in_url: False
#+ transform: {
#+   "description": "?info",
#+   "$anchor": "description",
#+   "@iot.count": "?total",
#+   "@iot.nextLink": "?next_link",
#+   "value": {
#+     "@iot.id": "?id",
#+     "@iot.selfLink": "?id_link",
#+     "phenomenonTime": "?phenomenonTime",
#+     "resultTime": "?resultTime",
#+     "result": {
#+        "densityOfJF": "?densityOfJF",
#+        "quantityOfJF": "?quantityOfJF",
#+        "sampleSizeValue": "?sampleSizeValue",
#+        "speciesScientificName": "?scientificName",
#+        "wormsConcept": "?wormsConcept",
#+        "stingByJF": "?stingByJF",
#+        "beachedJF": "?beachedJF",
#+     },
#+     "Datastream@iot.navigationLink": "?datastream_link",
#+     "FeatureOfInterest@iot.navigationLink": "?feature_link",
#+   }
#+ }

PREFIX iliad-api: <https://w3id.org/iliad/jf/api/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sosa:  <http://www.w3.org/ns/sosa/>
PREFIX oim-jf:  <http://w3id.org/iliad/oim/jellyfish#> 
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dwc-mbg: <http://mmisw.org/ont/ioos/marine_biogeography/>

SELECT DISTINCT ?info ?id ?id_link ?resultTime ?phenomenonTime ?densityOfJF ?quantityOfJF ?sampleSizeValue ?scientificName ?wormsConcept ?stingByJF ?beachedJF ?datastream_link ?feature_link ?total ?next_link
WHERE {
    ?col a sosa:ObservationCollection ;
      sosa:resultTime ?resultTime ;
			sosa:phenomenonTime ?phenomenonTime ; 
      sosa:madeBySensor ?sensor_URI ;
      sosa:observedProperty ?property_URI ;
      sosa:hasMember ?s .
    ?s a sosa:Observation ;
			sosa:hasResult ?result .
    OPTIONAL {
      ?result oim-jf:densityOfJF ?densityOfJF ;
        dwc:individualCount ?quantityOfJF ; 
			  dwc:sampleSizeValue ?sampleSizeValue ;
			  dwc:scientificName ?scientificName ;
			  dwc-mbg:aphiaID ?wormsConcept ;
        oim-jf:stingByJF ?stingByJF ;
	      oim-jf:beachedJF ?beachedJF .
    }
	BIND ("OGC SensorThings API - Observations" as ?info)	  
    BIND (REPLACE(STR(?s), "^.*/([^/]*)$", "$1") as ?id)
    BIND (REPLACE(STR(?sensor_URI), "^.*/([^/]*)$", "$1") as ?sensor)
    BIND (REPLACE(STR(?property_URI), "^.*/([^/]*)$", "$1") as ?property)
    OPTIONAL { ?s ?p ?_sensor }
    OPTIONAL { ?s ?p ?_property }
    FILTER (?sensor= ?_sensor && ?property=?_property)
    BIND (URI(CONCAT('https://w3id.org/iliad/jf/api/v1.0/Observations(', ?id,')')) AS ?id_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/jf/api/v1.0/Observations(', ?id,')/Datastream')) AS ?datastream_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/jf/api/v1.0/Observations(', ?id,')/FeautureOfInterest')) AS ?feature_link)
    {
       SELECT (count(*) as ?total) 
       WHERE { 
        ?col a sosa:ObservationCollection ;
          sosa:madeBySensor ?sensor_URI ;
          sosa:observedProperty ?property_URI ;
          sosa:hasMember ?s .
        ?s a sosa:Observation ;
			    sosa:hasResult ?result .
        BIND (REPLACE(STR(?sensor_URI), "^.*/([^/]*)$", "$1") as ?s1)
        BIND (REPLACE(STR(?property_URI), "^.*/([^/]*)$", "$1") as ?p1)
        OPTIONAL { ?s ?p ?_sensor }
        OPTIONAL { ?s ?p ?_property }
        FILTER (?s1= ?_sensor && ?p1=?_property)
      }
    }
    BIND (URI(CONCAT('https://w3id.org/iliad/jf/api/v1.0/Datastreams(',?sensor,'-',?property,')/Observations?$top=',xsd:integer(?_limit),'&$skip=', xsd:integer(COALESCE(?_offset, 0))+xsd:integer(?_limit))) AS ?next_link)
}
